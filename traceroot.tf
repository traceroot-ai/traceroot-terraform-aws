# deploy/terraform/aws/traceroot.tf
# Helm release for the TraceRoot application
# All Helm values inline from Terraform outputs

locals {
  app_url = var.domain != "" ? "https://${var.domain}" : ""

  # Core application values
  traceroot_values = <<-EOT
serviceAccount:
  create: true
  name: traceroot
  annotations:
    eks.amazonaws.com/role-arn: ${aws_iam_role.traceroot_irsa.arn}

imagePullPolicy: IfNotPresent

web:
  image:
    repository: ${aws_ecr_repository.services["web"].repository_url}
    tag: ${var.image_tag}
  replicas: ${var.web_replicas}
  port: 3000
  resources:
    requests:
      cpu: "250m"
      memory: "512Mi"
    limits:
      cpu: "1"
      memory: "1Gi"

rest:
  image:
    repository: ${aws_ecr_repository.services["rest"].repository_url}
    tag: ${var.image_tag}
  replicas: ${var.rest_replicas}
  port: 8000
  resources:
    requests:
      cpu: "1"
      memory: "2Gi"
    limits:
      cpu: "2"
      memory: "4Gi"

worker:
  image:
    repository: ${aws_ecr_repository.services["worker"].repository_url}
    tag: ${var.image_tag}
  replicas: ${var.worker_replicas}
  resources:
    requests:
      cpu: "250m"
      memory: "512Mi"
    limits:
      cpu: "1"
      memory: "1Gi"

billing:
  image:
    repository: ${aws_ecr_repository.services["billing"].repository_url}
    tag: ${var.image_tag}
  replicas: 1
  resources:
    requests:
      cpu: "128m"
      memory: "256Mi"
    limits:
      cpu: "500m"
      memory: "512Mi"

agent:
  image:
    repository: ${aws_ecr_repository.services["agent"].repository_url}
    tag: ${var.image_tag}
  replicas: 1
  port: 8100
  resources:
    requests:
      cpu: "250m"
      memory: "512Mi"
    limits:
      cpu: "1"
      memory: "1Gi"

detector:
  image:
    repository: ${aws_ecr_repository.services["detector"].repository_url}
    tag: ${var.image_tag}
  replicas: 1
  resources:
    requests:
      cpu: "250m"
      memory: "512Mi"
    limits:
      cpu: "1"
      memory: "1Gi"

migrations:
  postgres:
    image:
      repository: ${aws_ecr_repository.services["migrate-postgres"].repository_url}
      tag: ${var.image_tag}
  clickhouse:
    image:
      repository: ${aws_ecr_repository.services["migrate-clickhouse"].repository_url}
      tag: ${var.image_tag}

betterAuth:
  url: "${local.app_url}"

postgresql:
  host: "${aws_rds_cluster.postgres.endpoint}"
  database: "${local.database_name}"
  existingSecret: "traceroot"
  secretKeys:
    databaseUrl: "database-url"
    password: "postgres-password"

redis:
  existingSecret: "traceroot"
  secretKeys:
    url: "redis-url"

s3:
  bucket: "${aws_s3_bucket.traceroot.id}"
  region: "${var.aws_region}"
  endpoint: ""
  forcePathStyle: false

clickhouse:
  deploy: true
  image:
    repository: bitnamilegacy/clickhouse
  auth:
    username: default
    existingSecret: "traceroot"
    existingSecretKey: "clickhouse-password"
  replicaCount: ${var.clickhouse_replica_count}
  shards: 1
  zookeeper:
    enabled: false
  persistence:
    enabled: true
    size: ${var.clickhouse_storage_size}
    storageClass: "efs"
  resources:
    requests:
      cpu: "2"
      memory: "16Gi"
    limits:
      cpu: "2"
      memory: "16Gi"
  extraEnvVars:
    - name: MALLOC_CONF
      value: "background_thread:true,dirty_decay_ms:1000,muzzy_decay_ms:1000"

secrets:
  existingSecret: "traceroot"
  keys:
    betterAuthSecret: "better-auth-secret"
    internalApiSecret: "internal-api-secret"
    encryptionKey: "encryption-key"
EOT

  # Ingress values - conditional TLS
  ingress_values_https = <<-EOT
ingress:
  enabled: true
  className: alb
  host: "${var.domain}"
  annotations:
    alb.ingress.kubernetes.io/scheme: ${var.alb_scheme}
    alb.ingress.kubernetes.io/target-type: ip
    alb.ingress.kubernetes.io/listen-ports: '[{"HTTP":80},{"HTTPS":443}]'
    alb.ingress.kubernetes.io/ssl-redirect: "443"
    alb.ingress.kubernetes.io/certificate-arn: "${var.domain != "" ? aws_acm_certificate.app[0].arn : ""}"
EOT

  ingress_values_http = <<-EOT
ingress:
  enabled: true
  className: alb
  host: ""
  annotations:
    alb.ingress.kubernetes.io/scheme: ${var.alb_scheme}
    alb.ingress.kubernetes.io/target-type: ip
    alb.ingress.kubernetes.io/listen-ports: '[{"HTTP":80}]'
EOT

  ingress_values = var.domain != "" ? local.ingress_values_https : local.ingress_values_http

  # GitHub secret reference (conditional)
  github_values = var.github_app_id != "" ? "github:\n  existingSecret: \"traceroot-github\"" : ""

  # LLM keys secret reference (conditional)
  llm_values = (var.anthropic_api_key != "" || var.openai_api_key != "") ? "llmKeys:\n  existingSecret: \"traceroot-llm-keys\"" : ""

  # Stripe secret reference (conditional)
  stripe_values = var.stripe_secret_key != "" ? "stripe:\n  existingSecret: \"traceroot-stripe\"" : ""

  # Slack secret reference (conditional). Condition MUST match the
  # `kubernetes_secret.slack` count in secrets.tf — otherwise the Helm chart
  # references `traceroot-slack` while the secret was never created and pods
  # crash at startup.
  slack_values = (
    var.slack_client_id != "" && var.slack_client_secret != "" && var.domain != ""
    ? "slack:\n  existingSecret: \"traceroot-slack\""
    : ""
  )

  # Google OAuth secret reference (conditional)
  google_oauth_values = var.google_oauth_client_id != "" ? "googleOAuth:\n  existingSecret: \"traceroot-google-oauth\"" : ""

  # SMTP secret reference (conditional)
  smtp_values = var.smtp_url != "" ? "smtp:\n  existingSecret: \"traceroot-smtp\"" : ""

  # Enterprise license secret reference (conditional)
  enterprise_values = var.enterprise_license_key != "" ? "enterprise:\n  existingSecret: \"traceroot-enterprise\"" : ""

  # Feature flags
  feature_values = "enableBilling: \"${var.enable_billing}\""

  # Additional env vars (escape hatch)
  additional_env_values = length(var.additional_env) == 0 ? "" : <<EOT
additionalEnv:
%{for env in var.additional_env~}
    - name: ${env.name}
%{if env.value != null~}
      value: "${env.value}"
%{endif~}
%{if env.valueFrom != null~}
      valueFrom:
%{if env.valueFrom.secretKeyRef != null~}
        secretKeyRef:
          name: ${env.valueFrom.secretKeyRef.name}
          key: ${env.valueFrom.secretKeyRef.key}
%{endif~}
%{if env.valueFrom.configMapKeyRef != null~}
        configMapKeyRef:
          name: ${env.valueFrom.configMapKeyRef.name}
          key: ${env.valueFrom.configMapKeyRef.key}
%{endif~}
%{endif~}
%{endfor~}
EOT

  # System-log tables grow unbounded; their merges OOM-kill ClickHouse on a
  # memory-capped box (#963). When disabled (default), drop the high-volume tables
  # and keep query_log/part_log/error_log for debugging.
  clickhouse_log_table_overrides = var.enable_clickhouse_log_tables ? "" : <<EOT
clickhouse:
  extraOverrides: |
      <clickhouse>
        <trace_log remove="1"/>
        <text_log remove="1"/>
        <opentelemetry_span_log remove="1"/>
        <asynchronous_metric_log remove="1"/>
        <metric_log remove="1"/>
        <latency_log remove="1"/>
      </clickhouse>
EOT

  # The public SQL gateway runs customer SQL as a dedicated least-privileged
  # ClickHouse user against curated views owned by a separate writer, rather than
  # as the admin. Creating those identities, and setting the views' definer,
  # requires access management, which the stock admin does not carry. It is a
  # subchart setting, so the chart cannot switch it on from its own flag and it
  # is supplied here alongside it. The username matches clickhouse.auth.username.
  sql_gateway_values = !var.enable_sql_gateway ? "" : <<EOT
sqlGateway:
  enabled: true
clickhouse:
  usersExtraOverrides: |
      <clickhouse>
        <users>
          <default>
            <access_management>1</access_management>
          </default>
        </users>
      </clickhouse>
EOT
}

resource "helm_release" "traceroot" {
  name = "traceroot"

  # Published from github.com/traceroot-ai/traceroot-k8s via chart-releaser.
  # Pinned by version so an environment upgrades deliberately, never by drift.
  repository = var.traceroot_helm_chart_repository
  chart      = "traceroot"
  version    = var.traceroot_helm_chart_version

  namespace = kubernetes_namespace.app.metadata[0].name

  values = compact([
    local.traceroot_values,
    local.ingress_values,
    local.github_values,
    local.llm_values,
    local.stripe_values,
    local.slack_values,
    local.google_oauth_values,
    local.smtp_values,
    local.enterprise_values,
    local.feature_values,
    local.additional_env_values,
    local.clickhouse_log_table_overrides,
    local.sql_gateway_values,
  ])

  # Ensure global.security.allowInsecureImages is set for bitnamilegacy images
  set {
    name  = "global.security.allowInsecureImages"
    value = "true"
  }

  depends_on = [
    kubernetes_namespace.app,
    aws_iam_role.traceroot_irsa,
    aws_iam_role_policy.traceroot_s3_access,
    kubernetes_persistent_volume.clickhouse_data,
    helm_release.aws_lb_controller,
    kubernetes_secret.app,
    kubernetes_storage_class.efs,
    aws_acm_certificate_validation.app,
  ]
}
